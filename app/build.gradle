plugins {
    id 'com.android.application'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.weberbox.pifire"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        nonfirebase {
            dimension "version"
            buildConfigField("boolean", "IS_FIREBASE", "false")
            ext.useGoogleGcm = false
        }
        firebase {
            dimension "version"
            buildConfigField("boolean", "IS_FIREBASE", "true")
            ext.useGoogleGcm = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    applicationVariants.all { variant ->
        def appName
        if (project.hasProperty("applicationName")) {
            appName = applicationName
        } else {
            appName = parent.name
        }
        def dimension = variant.productFlavors[0].name
        def buildType = variant.buildType.name
        def version = variant.versionName

        variant.outputs.all { output ->
            outputFileName = "${appName}-${dimension}-${buildType}${version}.apk"
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.security:security-crypto:1.0.0'
    implementation 'com.pixplicity.easyprefs:EasyPrefs:1.10.0'
    implementation 'com.journeyapps:zxing-android-embedded:4.2.0'
    implementation 'nl.joery.animatedbottombar:library:1.1.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation "com.github.skydoves:powerspinner:1.1.9"
    testImplementation 'junit:junit:4.+'
    firebaseImplementation platform('com.google.firebase:firebase-bom:28.4.0')
    firebaseImplementation 'com.google.firebase:firebase-messaging'
    firebaseImplementation 'com.google.firebase:firebase-crashlytics'
    firebaseImplementation 'com.google.firebase:firebase-analytics'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation ('io.socket:socket.io-client:2.0.1') {
        exclude group: 'org.json', module: 'json'
    }

    apply plugin: 'com.android.application'
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.gms.google-services'

    afterEvaluate {
        android.productFlavors.each { flavor ->
            tasks.matching {
                it.name.contains('GoogleServices') && it.name.contains(flavor.name.capitalize())
            }*.enabled = flavor.ext.useGoogleGcm
        }
        android.productFlavors.each { flavor ->
            tasks.matching {
                it.name.contains('Crashlytics') && it.name.contains(flavor.name.capitalize())
            }*.enabled = flavor.ext.useGoogleGcm
        }
    }
}